plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'io.github.anomalyforlife'
version = providers.gradleProperty('pluginVersion').get()

def mcVersion = providers.gradleProperty('minecraftVersion').get()
def paperApiRevision = providers.gradleProperty('paperApiRevision').get()
def paperApiVersion = "${mcVersion}-${paperApiRevision}"
def runPaperMcVersion = mcVersion.tokenize('.').take(2).join('.')

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:${paperApiVersion}")

    implementation("com.google.code.gson:gson:2.10.1")
    implementation("com.zaxxer:HikariCP:5.1.0")
    implementation("org.xerial:sqlite-jdbc:3.45.3.0")
    implementation("com.mysql:mysql-connector-j:8.4.0")
}

tasks.shadowJar {
    archiveClassifier.set("")
    // NOTE: Relocation requires bytecode remapping. On some setups Shadow's ASM
    // can't read Java 21 class files (major version 65), causing build failures.
}

tasks.build {
    dependsOn tasks.shadowJar
}

tasks {
    runServer {
        minecraftVersion(runPaperMcVersion)
        pluginJars.from(tasks.shadowJar)
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
