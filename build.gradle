plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id("io.papermc.hangar-publish-plugin") version "0.1.2"
}

import java.io.ByteArrayOutputStream

group = 'io.github.anomalyforlife'
version = providers.gradleProperty('pluginVersion').get()

def mcVersion = providers.gradleProperty('minecraftVersion').get()
def paperApiRevision = providers.gradleProperty('paperApiRevision').get()
def paperApiVersion = "${mcVersion}-${paperApiRevision}"
def runPaperMcVersion = mcVersion.tokenize('.').take(2).join('.')

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:${paperApiVersion}")

    // Mantieni queste dipendenze SOLO per compilare (Paper le scaricherà via paper-plugin.yml)
    compileOnly "com.zaxxer:HikariCP:5.1.0"
    compileOnly "org.xerial:sqlite-jdbc:3.45.3.0"
    compileOnly "com.mysql:mysql-connector-j:8.3.0"
    compileOnly "com.google.code.gson:gson:2.10.1"
}

tasks.shadowJar {
    archiveClassifier.set("")
}

tasks.build {
    dependsOn tasks.shadowJar
}

tasks {
    runServer {
        minecraftVersion(runPaperMcVersion)
        pluginJars.from(tasks.shadowJar)
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

def executeGitCommand = { List<String> command ->
    try {
        def out = new ByteArrayOutputStream()
        def result = exec {
            commandLine command
            standardOutput = out
            errorOutput = out
            ignoreExitValue = true
        }
        if (result.exitValue != 0) {
            return ""
        }
        return out.toString("UTF-8").trim()
    } catch (Throwable ignored) {
        return ""
    }
}

def latestCommitMessage = { ->
    executeGitCommand(["git", "log", "-1", "--pretty=%B"])
}

def versionString = project.version.toString()
def isRelease = !versionString.contains('-')

def runNumber = System.getenv("GITHUB_RUN_NUMBER")
def suffixedVersion = (!isRelease && runNumber != null && !runNumber.isBlank())
        ? "${versionString}+${runNumber}"
        : versionString

def changelogContent = latestCommitMessage()
if (changelogContent == null || changelogContent.isBlank()) {
    changelogContent = "Automated publish."
}

hangarPublish {
    publications {
        register("plugin") {
            version.set(suffixedVersion)
            channel.set(isRelease ? "Release" : "Snapshot")
            changelog.set(changelogContent)
            id.set("hopperfilter")
            apiKey.set(System.getenv("HANGAR_API_TOKEN"))

            platforms {
                register(io.papermc.hangarpublishplugin.model.Platforms.PAPER) {
                    // Carica il jar "thin" su Hangar (molto più piccolo del shadowJar)
                    jar.set(tasks.named("jar").flatMap { it.archiveFile })

                    platformVersions.set(
                            (property("paperVersion") as String)
                                    .split(",")
                                    .collect { it.trim() }
                                    .findAll { !it.isBlank() }
                    )
                }
            }
        }
    }
}

tasks.named("jar").configure {
    doLast {
        def f = archiveFile.get().asFile
        def maxBytes = 15L * 1024L * 1024L
        if (f.length() > maxBytes) {
            throw new GradleException("Jar too large for Hangar (>15MB): ${f.name} is ${f.length()} bytes")
        }
    }
}
